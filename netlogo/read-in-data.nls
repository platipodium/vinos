; SPDX-FileCopyrightText: 2022 Universit√§t Hamburg (UHH)
; SPDX-FileCopyrightText: 2022 Helmholtz-Zentrum hereon GmbH (hereon)
; SPDX-License-Identifier: CC-BY-4.0
; Author: Sascha Hokamp <sascha.hokamp@uni-hamburg.de>
; Author: Carsten Lemmen <carsten.lemmen@hereon.de>


to read-landings [port-type]
  ifelse port-type = "home" [
    file-open "../data/220126-one_year-landings-per_port-home.csv"      ; Data of German fleet per home port
  ][
  file-open "../data/220126-one_year-landings-per_port-favorite.csv"  ; Data of German fleet per favorite landing port
  ] 
  gis:load-coordinate-system "../data/wgs1984.prj"

  ; skip the first row
  let row csv:from-row file-read-line

  set-default-shape ports "house"

  ; create home-ports

  while [ not file-at-end? ] [
    set row csv:from-row file-read-line

    if  (item 18 row) < 9.16 [
      ; In rows 17 and 18 there are decimal coordinates
      let xy gis:project-lat-lon (item 17 row) (item 18 row)

      ; Create ports only if on current map (list is not empty) and not Baltic (lon > 9.16)
      if not (length xy = 0) [
        create-ports 1 [
          set kind port-type
          set name item 15 row
          set country item 14 row
          set latitude item 17 row
          set longitude item 18 row
          
          set landings-euro n-values (number-of-species - 1) [ i -> item (5 + i) row ]
          set landings-euro lput (item 3 row - (reduce + landings-euro)) landings-euro
          set landings-kg n-values (number-of-species - 1) [ i -> item (8 + i) row ]
          set landings-kg lput (item 4 row - (reduce + landings-kg)) landings-kg
          set price n-values (number-of-species) [ i -> item i landings-euro / ifelse-value (item i landings-kg > 0) [item i landings-kg] [1]]
          set port-transportation-costs  sum landings-euro * fraction-transportation-costs
          set port-operation-costs sum landings-euro * operating-costs-of-boats
          set port-average-fishing-effort-in-days item 12 row
          set port-average-fishing-effort-in-hours item 13 row
          set port-average-trip-length  port-average-fishing-effort-in-hours * 18 ; assumption: set average speed of a vessel to 18 km per h
          set vessels-per-port item 11 row
          set size 4
          set label name
          set label-color black
          setxy (item 0 xy)  (item 1 xy)
          ; find a starting patch at least 3 nm from port away
          set start-patch min-one-of patches with [depth > navigable-depth and gis-distance myself > 5] [gis-distance myself] 
          set landing-patch start-patch
        ]
      ]
    ]
  ]
  file-close
  
  
end
