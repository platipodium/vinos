; SPDX-FileCopyrightText: 2022-2023 Helmholtz-Zentrum hereon GmbH
; SPDX-License-Identifier: Apache-2.0
; SPDX-FileContributor: Carsten Lemmen <carsten.lemmen@hereon.de>

; Simple calendar implementation using the time extension

extensions [ time ]

globals [
  date ; a logotime object representing absolute date
  holidays-germany ; a logotime series with dates and names of holidays
  holidays-morocco ; a logotime series with dates and names of holidays
  holiday?
]

to setup-calendar

  ; for localization purposes, get the full month names
  let _month-names n-values 12 [ i ->
    time:show  time:create (word "2023/" (i + 1) "/01") "MMMM"
  ]
  let _time-formats (list "d-" "d-M" "d-MM" "dd-" "dd-M" "dd-MM" )
  set _time-formats map [ _tf -> (word _tf "MM-yyyy") ] _time-formats

  set days-in-months (list 31 28 31 30 31 30 31 31 30 31 30 31 )

  set date logodate-from-datetime

  ; Now add to the current time the offset
  set date time:plus date time-offset "months"

  set year time:get "year" date
  set month time:get "month" date
  set day time:get "day"	date
  set day-of-year time:get "dayofyear" date

  set holidays-germany ts-load "../data/holidays/holidays_DE.csv"
  set holidays-morocco ts-load "../data/holidays/holidays_MA.csv"

  set holiday? is-holiday?
  ;set holidays-germany ts-load-dates "../data/holidays/holidays_DE.csv"
  ;set holidays-morocco ts-load-dates "../data/holidays/holidays_MA.csv"

end

; Advance the calendar by one day
to advance-calendar

  set date time:plus date 1 "days"

  set year time:get "year" date
  set month time:get "month" date
  set day time:get "day"	date
  set day-of-year time:get "dayofyear" date

  set holiday? is-holiday?

end

to-report datetime
  report (word time:show date "EEE yyyy-MM-dd" " (" time:get "dayofyear" date ")")
end

to-report datetime-short
  report time:show date "yyyyMMdd"
end

to-report is-holiday?
  report ts-member? holidays-germany date
end


to-report logodate-from-format [ date-string format-string ]
  let _logodate nobody
  carefully [
    set _logodate time:create-with-format date-string format-string
  ][
  ]
  report _logodate
end

to-report logodate-from-datetime
  ; date-and-time gives a locale-dependent and JVM precision-dependent
  ; output.  The YMD is the last part of the string in format
  ; dd-MMM-yyyy  (MMM as a localized three-letter string)
  ; but on some systems, d is single digits and M is MM or MMM
  ; The date-and-time string's date information is the last item of a
  ; space-separated string

  let  _ymd last split " " date-and-time
  set _ymd split "-" _ymd
  let _format "d"
  if length item 0 _ymd > 1 [set _format (word _format "d")]
  set _format (word _format "-MM")
  if length item 1 _ymd > 2 [set _format (word _format "M")]
  if length item 1 _ymd > 3 [set _format (word _format "M")]
  set _format (word _format "-YYYY")
  report time:create-with-format (last split " " date-and-time) _format
end
