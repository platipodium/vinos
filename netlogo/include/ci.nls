; SPDX-FileCopyrightText: 2023 Helmholtz-Zentrum hereon GmbH
; SPDX-License-Identifier: Apache-2.0
; SPDX-FileContributor: Carsten Lemmen <carsten.lemmen@hereon.de>
; Routines that are called by CI

to reset-patches
  setup-monthly
  ask water-patches [
    set prey-catches 0
    set cluster-fishing-efforts 0
    set cluster-prey-catches  0
  ]
end

to reset-date [_arg]
  set date logodate _arg
  setup-calendar

  ask actions [die]

  reset-patches
  ask boats [
    set boat-total-days-at-sea 0
    set boat-total-fuel-consumption 0
    set boat-total-landings 0
  ]
end

; This routine is to be called by CI, so after a setup is
; automatically performed
to ci-maps

  ;ask links   [set hidden? true]
  ;ask turtles [set hidden? true]
  set show-values? false
  set show-ports? false
  set show-boats? false
  set box? true
  set owf? false
  set one? false
  update-drawings

  ci-run-year 2020
  ci-run-year 2030

end

to save-tides
  set scene "Tide"
  let _dataset-name "Tide"
  let _ndays  2
  let _nsteps  48

  let _csv  (word "results/tide_gauge_cuxhaven_" (time:get "year" datetime)
      "-"  (time:get "month" datetime) "-" (time:get "day" datetime) ".csv")
  carefully [file-delete _csv][]

  ; Reference data are from Cuxhaven Steubenh√∂ft at 53.86827498075895 8.717586224283938
  let _coords gis:project-lat-lon 53.86827498075895 8.717586224283938
  let _cuxhaven-patch  patch (1 + item 0 _coords)  (1 + item 1 _coords) ; move 1 northeast, as Cuxhaven patch itself has no water

  file-open _csv
  file-print (word "# year,month,day,hour,minute,level")
  file-print (word "#     ,     ,   ,    ,      , m   ")
  file-close

  repeat _ndays * _nsteps [
    update-scene
    ask date-patch [set plabel datetime-long]

    let _name (word "results" "/" (lower _dataset-name) "_" datetime-short)
    export-view (word _name ".png")
    gis:store-dataset gis:patch-dataset temporary (word _name)

    let _level [temporary] of _cuxhaven-patch

    file-open _csv
    file-print csv:to-row (list
      (time:get "year" datetime) (time:get "month" datetime) (time:get "day" datetime)
      (time:get "hour" datetime) (time:get "minute" datetime) _level
    )
    file-close

    tick-advance (1 / _nsteps)
  ]
end

to ci-spinup-year [ _arg ]
  let _spinup-days 100
  let _date time:plus (logodate _arg) (- _spinup-days) "days"

  reset-date _date
  reset-ticks
  update-date-patch
  print (word "Running spinup simulation for 365 days starting " (time:show _date "YYYY-mm-DD") " ..")
  ;repeat 60 [ go ]
end

to ci-run-year [ _arg ]
  let _date logodate _arg
  ci-spinup-year _date
  save-dynamic-dataset "Effort MWh"
  print (word "Running production simulation for 365 days starting " (time:show _date "YYYY-mm-DD") " ..")
  ;repeat 365 [ go ]
  save-dynamic-dataset "Effort MWh"
end
