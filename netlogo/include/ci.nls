; SPDX-FileCopyrightText: 2023 Helmholtz-Zentrum hereon GmbH
; SPDX-License-Identifier: Apache-2.0
; SPDX-FileContributor: Carsten Lemmen <carsten.lemmen@hereon.de>
; Routines that are called by CI

to reset-year [_year]

  set date time:create (word _year "-01-01")
  setup-calendar

  ask actions [die]
  ask patches [
    set patch-effort-hours 0
    set patch-effort-mwatthours 0
    set prey-catches 0
    set cluster-fishing-efforts 0
    set cluster-prey-catches  0
    set swept-area 0
  ]

  ask boats [
    set boat-total-days-at-sea 0
    set boat-total-fuel-consumption 0
    set boat-total-landings 0
  ]


end


; This routine is to be called by CI, so after a setup is
; automatically performed
to ci-maps

  ;ask links   [set hidden? true]
  ;ask turtles [set hidden? true]
  set show-values? false
  set show-ports? false
  set show-boats? false
  set box? true
  set owf? false
  update-drawings

  reset-year 2020
  reset-ticks
  update-date-patch
  repeat 365 [
    go
    ;print date
  ]
  save-dynamic-dataset "Effort"

  reset-year 2030
  reset-ticks
  update-date-patch
  repeat 365 [
    go
    ;print date
  ]
  save-dynamic-dataset "Effort"

end

; This routine is to be called by CI and is deprecated
to create-effort-map

  ask patches [
    set patch-effort-hours 0
    set patch-effort-mwatthours 0
  ]

  repeat 365 [ go ]

  ; data storage
  let prefix  (word "results/effort-" substring date-and-time 0 12)

  set scene "Effort h"
  update-scene
  clear-drawing
  ask links [set hidden? true]
  ask boats [set hidden? true]

  export-view (word prefix ".png")

  ask links [set hidden? false]
  ask boats [set hidden? false]

  let dataset gis:patch-dataset patch-effort-hours
  gis:store-dataset dataset prefix

end

to save-tides
  set scene "Tide"
  let _dataset-name "Tide"
  let _ndays  2
  let _nsteps  48

  let _csv  (word "results/tide_gauge_cuxhaven_" (time:get "year" datetime)
      "-"  (time:get "month" datetime) "-" (time:get "day" datetime) ".csv")
  carefully [file-delete _csv][]

  ; Reference data are from Cuxhaven Steubenh√∂ft at 53.86827498075895 8.717586224283938
  let _coords gis:project-lat-lon 53.86827498075895 8.717586224283938
  let _cuxhaven-patch  patch (1 + item 0 _coords)  (1 + item 1 _coords) ; move 1 northeast, as Cuxhaven patch itself has no water

  file-open _csv
  file-print (word "# year,month,day,hour,minute,level")
  file-print (word "#     ,     ,   ,    ,      , m   ")
  file-close

  repeat _ndays * _nsteps [
    update-scene
    ask date-patch [set plabel datetime-long]

    let _name (word "results" "/" (lower _dataset-name) "_" datetime-short)
    export-view (word _name ".png")
    gis:store-dataset gis:patch-dataset temporary (word _name)

    let _level [temporary] of _cuxhaven-patch

    file-open _csv
    file-print csv:to-row (list
      (time:get "year" datetime) (time:get "month" datetime) (time:get "day" datetime)
      (time:get "hour" datetime) (time:get "minute" datetime) _level
    )
    file-close

    tick-advance (1 / _nsteps)
  ]
end
