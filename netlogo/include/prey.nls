; SPDX-FileCopyrightText: 2023-2024 Helmholtz-Zentrum hereon GmbH
; SPDX-License-Identifier: Apache-2.0
; SPDX-FileContributor: Carsten Lemmen <carsten.lemmen@hereon.de>

extensions [ csv ]
breed [preys prey]

preys-own [
  prey-name         ; name, here the latin Uppercase englsish name, e.g. Shrimp, Plaice, ...
  prey-scientific        ; scientific name, e.g. Crangon crangon
  prey-english
  prey-german
  prey-three-code     ; FAO 3 code, e.g CSH for crangon
  prey-picture
]

globals [
  prey-names
  prey-number
  prey-prices
]

; We define here a constant and predefined number of gears
to setup-preys

  read-prey-csv

  ask preys [
    if prey-name = "Shrimp" [ set prey-german "Nordseekrabbe" set prey-picture "../figures/320px-Crangon_crangon.jpg" ]
    if prey-name = "Plaice" [ set prey-german "Scholle" set prey-picture "../figures/319px-Pleuronectes_platessa.jpg" ]
    if prey-name = "Sole" [ set prey-german "Seezunge" set prey-picture "../figures/Solea_solea_1.jpg" ]
    if prey-name = "Whiting" [ set prey-german "Wittling" set prey-picture "../figures/320px-Wijting002.jpg" ]
    if prey-name = "Sprat" [ set prey-german "Sprotte" set prey-picture "../figures/320px-Sprattus_sprattus.jpg" ]
    if prey-name = "Dab" [ set prey-german "Kliesche" set prey-picture "../figures/319px-Limanda_limanda.jpg" ]
    if prey-name = "Lobster" [ set prey-german "Kaisergranat" set prey-picture "../figures/320px-Nephrops_norvegicus.jpg" ]
  ]

  ; Report global variables, make sure to keep the order of the preys
  ; and the correct relation between species and gear name

  let my-prey-ids sort [who] of preys
  set prey-number count preys
  set prey-names n-values prey-number [i -> [prey-name] of prey item i my-prey-ids]
  set prey-prices n-values prey-number [ i -> 5] ; default value is 5 EUR kg-1
  set prey-prices replace-item (position "Shrimp" prey-names) prey-prices 7
  set prey-prices replace-item (position "Plaice" prey-names) prey-prices 1 ; should be lower
  set prey-prices replace-item (position "Sole" prey-names) prey-prices 11
end

to read-prey-csv

  if any? preys [ ask preys [
    ask my-links [die]
    die
  ]]

  file-open "./include/prey.csv"
  ; Read the first row without doing anything, the ASFIS column headers are
  ; "3A_Code","Taxonomic_Code","CPC_Group","Identifier","Name_En","Name_Fr","Name_Es","Name_Ar","Name_Cn",
  ; "Name_Ru","Scientific_Name","Author","Major_Group","Yearbook_Group","ISSCAAP_Group","CPC_Class","short_name"
  ; and were extended by the last column "short_name"
  let row csv:from-row file-read-line

  while [ not file-at-end? ] [
    set row csv:from-row file-read-line

    create-preys 1 [
      set prey-name last row
      set prey-three-code item 0 row
      set prey-english item 4 row
      set prey-scientific item 10 row
      set prey-german ""
      set prey-picture ""
      set hidden? true
    ]
  ]

  let my-prey-ids sort [who] of preys
  set prey-number count preys
  set prey-names n-values prey-number [i -> [prey-name] of prey item i my-prey-ids]

  file-close
  print (word "Created " prey-number " preys: " prey-names)

end

to-report prey-gear-indices
  ; gear-prey-names ["Shrimp" "Plaice" "Sole" "Plaice" "Sole" "Plaice"]
  ; prey-names  ["Lobster" "Shrimp" "Whiting" "Plaice" "Sole" "Dab" "Sprat"]

  report n-values count preys [ iprey ->
    filter [ igear -> (item igear gear-prey-names) = (item iprey prey-names) ] (range count gears)
  ]
end
